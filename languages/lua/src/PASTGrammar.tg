# Copyright (C) 2006-2007, The Perl Foundation.
# $Id$

=head1 NAME

PASTGrammar.tg - A grammar for transforming the output of a PGE parse
into an abstract syntax tree (PAST).

=head1 DESCRIPTION


=cut

grammar Lua::PAST::Grammar is TGE::Grammar;


transform past (ROOT) :language('PIR') {
    $P0 = new .Integer
    set $P0, 0
    set_hll_global ['Lua::PAST'], '$looplevel', $P0
    .local pmc past
    $P0 = new 'Lua::Symbtab'
    $P0.'push_scope'()
    set_hll_global ['Lua::PAST'], '$symbtab', $P0
    $P0 = node['block']
    past = tree.'get'('past', $P0, 'Lua::Grammar::block')
    .return past.'new'('PAST::Block', past, 'node'=>node, 'name'=>'_main', 'pragma'=>':anon :lex')
}


transform past (Lua::Grammar::block) :language('PIR') {
    .local pmc past
    past = new 'PAST::Stmts'
    past.'init'('node'=>node)
    $P0 = node['statement']
    if null $P0 goto L1
    .local pmc iter
    iter = new .Iterator, $P0
  L2:
    unless iter goto L1
    $P0 = shift iter
    $P1 = tree.'get'('past', $P0, 'Lua::Grammar::statement')
    past.'push'($P1)
    goto L2
  L1:
    $P0 = node['last_statement']
    if null $P0 goto L3
    $P0 = shift $P0
    $P1 = tree.'get'('past', $P0, 'Lua::Grammar::statement')
    past.'push'($P1)
  L3:
    .local pmc symbtab
    symbtab = get_hll_global ['Lua::PAST'], '$symbtab'
    symbtab.'pop_scope'()
    .return (past)
}


transform past (Lua::Grammar::statement) :language('PIR') {
    $S0 = node.'find_key'()
    $P0 = node[$S0]
    $S0 = concat 'Lua::Grammar::', $S0
    .return tree.'get'('past', $P0, $S0)
}


transform past (Lua::Grammar::do_block) :language('PIR') {
    .local pmc symbtab
    symbtab = get_hll_global ['Lua::PAST'], '$symbtab'
    symbtab.'push_scope'()
    $P0 = node['block']
    .return tree.'get'('past', $P0, 'Lua::Grammar::block')
}


transform past (Lua::Grammar::if_stat) :language('PIR') {
    .local pmc past, cpast, expr, blk
    $P0 = node['expression']
    expr = tree.'get'('past', $P0, 'Lua::Grammar::expression')
    .local pmc symbtab
    symbtab = get_hll_global ['Lua::PAST'], '$symbtab'
    symbtab.'push_scope'()
    $P0 = node['block']
    blk = tree.'get'('past', $P0, 'Lua::Grammar::block')
    past = expr.'new'('PAST::Op', expr, blk, 'node'=>node, 'pasttype'=>'if')
    cpast = past
    $P0 = node['elseif_block']
    if null $P0 goto L1
    .local pmc iter
    iter = new .Iterator, $P0
  L2:
    unless iter goto L1
    $P0 = shift iter
    $P1 = tree.'get'('past', $P0, 'Lua::Grammar::elseif_block')
    cpast.'push'($P1)
    cpast = $P1
    goto L2
  L1:
    $P0 = node['else_block']
    if null $P0 goto L3
    symbtab.'push_scope'()
    $P0 = shift $P0
    $P0 = $P0['block']
    $P1 = tree.'get'('past', $P0, 'Lua::Grammar::block')
    cpast.'push'($P1)
  L3:
    .return (past)
}


transform past (Lua::Grammar::elseif_block) :language('PIR') {
    .local pmc expr, blk
    $P0 = node['expression']
    expr = tree.'get'('past', $P0, 'Lua::Grammar::expression')
    .local pmc symbtab
    symbtab = get_hll_global ['Lua::PAST'], '$symbtab'
    symbtab.'push_scope'()
    $P0 = node['block']
    blk = tree.'get'('past', $P0, 'Lua::Grammar::block')
    .return expr.'new'('PAST::Op', expr, blk, 'node'=>node, 'pasttype'=>'if')
}


transform past (Lua::Grammar::while_stat) :language('PIR') {
    .local pmc looplevel
    looplevel = get_hll_global ['Lua::PAST'], '$looplevel'
    inc looplevel
    .local pmc expr, blk
    $P0 = node['expression']
    expr = tree.'get'('past', $P0, 'Lua::Grammar::expression')
    .local pmc symbtab
    symbtab = get_hll_global ['Lua::PAST'], '$symbtab'
    symbtab.'push_scope'()
    $P0 = node['block']
    blk = tree.'get'('past', $P0, 'Lua::Grammar::block')
    dec looplevel
    .return expr.'new'('PAST::Op', expr, blk, 'node'=>node, 'pasttype'=>'while')
}


transform past (Lua::Grammar::repeat_stat) :language('PIR') {
    .local pmc looplevel
    looplevel = get_hll_global ['Lua::PAST'], '$looplevel'
    inc looplevel
    .local pmc expr, blk
    .local pmc symbtab
    symbtab = get_hll_global ['Lua::PAST'], '$symbtab'
    symbtab.'push_scope'()
    $P0 = node['block']
    blk = tree.'get'('past', $P0, 'Lua::Grammar::block')
    $P0 = node['expression']
    expr = tree.'get'('past', $P0, 'Lua::Grammar::expression')
    dec looplevel
    .return expr.'new'('PAST::Op', blk, expr, 'node'=>node, 'pasttype'=>'repeat')
}


transform past (Lua::Grammar::for_stat) :language('PIR') {
    .local pmc looplevel
    looplevel = get_hll_global ['Lua::PAST'], '$looplevel'
    inc looplevel
    .local pmc past
    $S0 = node.'find_key'()
    $P0 = node[$S0]
    $S1 = concat 'Lua::Grammar::', $S0
    past = tree.'get'('past', $P0, $S1)
    $P0 = node['for_body']
    $P0 = $P0['block']
    $P1 = tree.'get'('past', $P0, 'Lua::Grammar::block')
    dec looplevel
    past.'push'($P1)
    .return (past)
}


transform past (Lua::Grammar::fornum) :language('PIR') {
    .local pmc name, explist
    .local pmc symbtab
    symbtab = get_hll_global ['Lua::PAST'], '$symbtab'
    symbtab.'push_scope'()
    $P0 = node['name']
    $S0 = $P0.'result_object'()
    symbtab.'insert'($S0)
    name = new 'PAST::Var'
    name.'init'('node'=>node, 'name'=>$S0, 'scope'=>'lexical', 'ismy'=>1, 'islvalue'=>1)
    explist = new .ResizablePMCArray
    $P0 = node['expression']
    $I0 = $P0
    .local pmc iter
    iter = new .Iterator, $P0
  L1:
    unless iter goto L2
    $P0 = shift iter
    $P1 = tree.'get'('past', $P0, 'Lua::Grammar::expression')
    push explist, $P1
    goto L1
  L2:
    unless $I0 < 3 goto L3
    $P1 = name.'new'('PAST::Val', 'node'=>node, 'vtype'=>'.LuaNumber', 'name'=>'1', 'ctype'=>'n+')
    push explist, $P1
  L3:
    .return name.'new'('PAST::Op', name, explist, 'node'=>node, 'pasttype'=>'fornum')
}


transform past (Lua::Grammar::forlist) :language('PIR') {
    .local pmc past, namelist, explist
    .local pmc symbtab
    symbtab = get_hll_global ['Lua::PAST'], '$symbtab'
    symbtab.'push_scope'()
    namelist = new .ResizablePMCArray
    $P0 = node['name_list']
    $P0 = $P0['name']
    .local pmc iter
    iter = new .Iterator, $P0
  L1:
    unless iter goto L2
    $P0 = shift iter
    $S0 = $P0.'result_object'()
    symbtab.'insert'($S0)
    past = new 'PAST::Var'
    past.'init'('node'=>node, 'name'=>$S0, 'scope'=>'lexical', 'ismy'=>1, 'islvalue'=>1)
    push namelist, past
    goto L1
  L2:
    $P0 = node['expression_list']
    explist = tree.'get'('explist', $P0, 'Lua::Grammar::expression_list')
    .return past.'new'('PAST::Op', namelist, explist, 'node'=>node, 'pasttype'=>'forlist')
}


transform past (Lua::Grammar::local_stat) :language('PIR') {
    $S0 = node.'find_key'()
    $P0 = node[$S0]
    $S1 = concat 'Lua::Grammar::', $S0
    .return tree.'get'('past', $P0, $S1)
}


transform past (Lua::Grammar::local_declaration) :language('PIR') {
    .local pmc past, namelist, explist
    .local pmc symbtab
    symbtab = get_hll_global ['Lua::PAST'], '$symbtab'
    $P0 = node['name']
    namelist = new .ResizablePMCArray
    .local pmc iter
    iter = new .Iterator, $P0
  L1:
    unless iter goto L2
    $P0 = shift iter
    $S0 = $P0.'result_object'()
    symbtab.'insert'($S0)
    past = new 'PAST::Var'
    past.'init'('node'=>node, 'name'=>$S0, 'scope'=>'lexical', 'ismy'=>1, 'islvalue'=>1)
    push namelist, past
    goto L1
  L2:
    $P0 = node['expression_list']
    if null $P0 goto L3
    $P0 = shift $P0
    explist = tree.'get'('explist', $P0, 'Lua::Grammar::expression_list')
  L3:
    unless null explist goto L4
    explist = new .ResizablePMCArray
  L4:
    past = new 'PAST::Op'
    past.'init'(namelist, explist, 'node'=>node, 'pasttype'=>'assignlist')
    .return (past)
}


transform past (Lua::Grammar::local_function) :language('PIR') {
    .local pmc past, var
    .local pmc symbtab
    symbtab = get_hll_global ['Lua::PAST'], '$symbtab'
    $P0 = node['function_body']
    past = tree.'get'('past', $P0, 'Lua::Grammar::function_body')
    $P0 = node['name']
    $S0 = $P0.'result_object'()
    symbtab.'insert'($S0)
    past.'name'($S0)
    var = past.'new'('PAST::Var', 'node'=>$P0, 'name'=>$S0, 'scope'=>'lexical', 'ismy'=>1, 'islvalue'=>1)
    .return past.'new'('PAST::Op', var, past, 'node'=>node, 'pasttype'=>'assign')
}


transform past (Lua::Grammar::return_statement) :language('PIR') {
    .local pmc past
    past = new 'PAST::Op'
    $P0 = node['expression_list']
    if null $P0 goto L1
    $P0 = shift $P0
    $P1 = tree.'get'('explist', $P0, 'Lua::Grammar::expression_list')
    past.'init'($P1 :flat, 'node'=>node, 'pasttype'=>'return')
    .return (past)
  L1:
    past.'init'('node'=>node, 'pasttype'=>'return')
    .return (past)
}


transform past (Lua::Grammar::break_statement) :language('PIR') {
    .local pmc looplevel
    looplevel = get_hll_global ['Lua::PAST'], '$looplevel'
    if looplevel <= 0 goto L1
    .local pmc past
    past = new 'PAST::Op'
    past.'init'('node'=>node, 'pasttype'=>'break')
    .return (past)
  L1:
    node.'syntax_error'("no loop to break")
}


transform past (Lua::Grammar::expression_stat) :language('PIR') {
    $P0 = node['assignment']
    unless null $P0 goto L1
    $P0 = node['primary_expression']
    .return tree.'get'('past', $P0, 'Lua::Grammar::primary_expression')
  L1:
    .local pmc varlist, explist
    $P0 = shift $P0
    explist = tree.'get'('explist', $P0, 'Lua::Grammar::assignment')
    varlist = tree.'get'('varlist', $P0, 'Lua::Grammar::assignment')
    $P0 = node['primary_expression']
    $P1 = tree.'get'('past', $P0, 'Lua::Grammar::primary_expression')
    $P1.'islvalue'(1)
    unshift varlist, $P1
    .local pmc past
    past = new 'PAST::Op'
    past.'init'(varlist, explist, 'node'=>node, 'pasttype'=>'assignlist')
    .return (past)
}


transform varlist (Lua::Grammar::assignment) :language('PIR') {
    .local pmc varlist
    $P0 = node['expression_list']
    if null $P0 goto L1
    varlist = new .ResizablePMCArray
    .return (varlist)
  L1:
    $P0 = node['assignment']
    varlist = tree.'get'('varlist', $P0, 'Lua::Grammar::assignment')
    $P0 = node['primary_expression']
    $P1 = tree.'get'('past', $P0, 'Lua::Grammar::primary_expression')
    $P1.'islvalue'(1)
    unshift varlist, $P1
    .return (varlist)
}


transform explist (Lua::Grammar::assignment) :language('PIR') {
    $P0 = node['expression_list']
    if null $P0 goto L1
    .return tree.'get'('explist', $P0, 'Lua::Grammar::expression_list')
  L1:
    $P0 = node['assignment']
    .return tree.'get'('explist', $P0, 'Lua::Grammar::assignment')
}


transform explist (Lua::Grammar::function_args) :language('PIR') {
    .local pmc explist, past
    $P0 = node['constructor']
    if null $P0 goto L1
    past = tree.'get'('past', $P0, 'Lua::Grammar::constructor')
    explist = new .ResizablePMCArray
    push explist, past
    .return (explist)
  L1:
    $P0 = node['string']
    if null $P0 goto L2
    past = tree.'get'('past', $P0, 'Lua::Grammar::string')
    explist = new .ResizablePMCArray
    push explist, past
    .return (explist)
  L2:
    $P0 = node['expression_list']
    unless null $P0 goto L3
    explist = new .ResizablePMCArray
    .return (explist)
  L3:
    $P0 = shift $P0
    .return tree.'get'('explist', $P0, 'Lua::Grammar::expression_list')
}


transform past (Lua::Grammar::function_stat) :language('PIR') {
    .local pmc past, var
    $P0 = node['functionname']
    var = tree.'get'('past', $P0, 'Lua::Grammar::functionname')
    $S0 = $P0.'result_object'()
    $I0 = index $S0, ':'
    if $I0 < 0 goto L1
    past = new 'PAST::Var'
    past.'init'('node'=>$P0, 'name'=>'self', 'scope'=>'parameter')
    set_hll_global ['Lua::PAST'], '$?self', past
  L1:
    $S0 = tree.'get'('name', $P0, 'Lua::Grammar::functionname')
    $P0 = node['function_body']
    past = tree.'get'('past', $P0, 'Lua::Grammar::function_body')
    past.'name'($S0)
    .return past.'new'('PAST::Op', var, past, 'node'=>node, 'pasttype'=>'assign')
}


transform name (Lua::Grammar::functionname) :language('PIR') {
    .local string name
    name = ''
    $P0 = node['name']
    .local pmc iter
    iter = new .Iterator, $P0
  L2:
    unless iter goto L1
    if name == '' goto L3
    name = concat '.'
  L3:
    $P0 = shift iter
    $S0 = $P0.'result_object'()
    name = concat $S0
    goto L2
  L1:
    .return (name)
}


transform past (Lua::Grammar::functionname) :language('PIR') {
    .local pmc past, iter, key
    $P0 = node['name']
    iter = new .Iterator, $P0
    $P0 = shift iter
    $S0 = $P0.'result_object'()
    past = new 'PAST::Var'
    past.'init'('node'=>node, 'name'=>$S0, 'scope'=>'package')
  L2:
    unless iter goto L1
    $P0 = shift iter
    $S0 = $P0.'result_object'()
    key = past.'new'('PAST::Val', 'node'=>$P0, 'vtype'=>'.LuaString', 'name'=>$S0, 'ctype'=>'s~')
    past = past.'new'('PAST::Var', past, key, 'node'=>node, 'scope'=>'keyed')
    goto L2
  L1:
    past.'islvalue'(1)
    .return (past)
}


transform past (Lua::Grammar::function_body) :language('PIR') {
    .local pmc past, parlist, var
    .local pmc symbtab
    symbtab = get_hll_global ['Lua::PAST'], '$symbtab'
    symbtab.'push_scope'()
    parlist = new .ResizablePMCArray
    var = get_hll_global ['Lua::PAST'], '$?self'
    if null var goto L1
    push parlist, var
    null var
    set_hll_global ['Lua::PAST'], '$?self', var
    symbtab.'insert'('self')
  L1:
    $P0 = node['parameter_list']
    if null $P0 goto L2
    $P1 = $P0['name_list']
    if null $P1 goto L3
    $P1 = $P1['name']
    .local pmc iter
    iter = new .Iterator, $P1
  L4:
    unless iter goto L3
    $P1 = shift iter
    $S0 = $P1.'result_object'()
    symbtab.'insert'($S0)
    var = new 'PAST::Var'
    var.'init'('node'=>$P1, 'name'=>$S0, 'scope'=>'parameter')
    push parlist, var
    goto L4
  L3:
    $P1 = $P0['vararg']
    $I0 = 0
    if null $P1 goto L2
    $P1 = shift $P1
    var = new 'PAST::Var'
    var.'init'('node'=>$P1, 'name'=>'vararg', 'scope'=>'parameter', 'isslurpy'=>1)
    push parlist, var
    $I0 = 1
    goto L5
  L2:
    var = new 'PAST::Var'
    var.'init'('node'=>$P0, 'name'=>'extra', 'scope'=>'parameter', 'isslurpy'=>1)
    push parlist, var
  L5:
    .local pmc has_vararg
    has_vararg = get_hll_global ['Lua::PAST'], '?vararg'
    $P0 = new .Boolean
    set $P0, $I0
    set_hll_global ['Lua::PAST'], '?vararg', $P0
    .local pmc looplevel
    looplevel = get_hll_global ['Lua::PAST'], '$looplevel'
    $P0 = new .Integer
    set $P0, 0
    set_hll_global ['Lua::PAST'], '$looplevel', $P0
    $P0 = node['block']
    past = tree.'get'('past', $P0, 'Lua::Grammar::block')
    set_hll_global ['Lua::PAST'], '$looplevel', looplevel
    set_hll_global ['Lua::PAST'], '?vararg', has_vararg
    .return past.'new'('PAST::Block', parlist :flat, past, 'node'=>node)
}


transform past (Lua::Grammar::constructor) :language('PIR') {
    .local pmc past
    new past, 'PAST::Val'
    past.'init'('node'=>node, 'vtype'=>'.LuaTable')
    $P0 = node['tablefieldlist']
    if null $P0 goto L1
    $P0 = shift $P0
    $P0 = $P0['tablefield']
    .local pmc iter, key, expr, var
    $I0 = 1
    iter = new .Iterator, $P0
  L2:
    unless iter goto L1
    $P0 = shift iter
    $S0 = $P0.'find_key'()
    $P0 = $P0[$S0]
    unless $S0 == 'expression' goto L3
    $P1 = tree.'get'('past', $P0, 'Lua::Grammar::expression')
    past.'push'($P1)
    goto L2
  L3:
    $S0 = concat 'Lua::Grammar::', $S0
    $P1 = tree.'get'('record', $P0, $S0)
    past.'push'($P1)
    goto L2
  L1:
    .return (past)
}


transform record (Lua::Grammar::record_field) :language('PIR') {
    .local pmc record
    record = new .FixedPMCArray
    set record, 2
    $S0 = node.'find_key'()
    $P0 = node[$S0]
    $S0 = concat 'Lua::Grammar::', $S0
    $P1 = tree.'get'('key', $P0, $S0)
    record[0] = $P1
    $P0 = node['expression']
    $P1 = tree.'get'('past', $P0, 'Lua::Grammar::expression')
    record[1] = $P1
    .return (record)
}


transform key (Lua::Grammar::name) :language('PIR') {
    .local pmc past
    $S0 = node.'result_object'()
    past = new 'PAST::Val'
    past.'init'('node'=>node, 'vtype'=>'.LuaString', 'name'=>$S0, 'ctype'=>'s~')
    .return (past)
}


transform key (Lua::Grammar::index) :language('PIR') {
    $P0 = node['expression']
    .return tree.'get'('past', $P0, 'Lua::Grammar::expression')
}


transform explist (Lua::Grammar::expression_list) :language('PIR') {
    .local pmc explist
    explist = new .ResizablePMCArray
    $P0 = node['expression']
    .local pmc iter
    iter = new .Iterator, $P0
  L1:
    unless iter goto L2
    $P0 = shift iter
    $P1 = tree.'get'('past', $P0, 'Lua::Grammar::expression')
    push explist, $P1
    goto L1
  L2:
    .return (explist)
}


transform past (Lua::Grammar::expression) :language('PIR') {
    $P0 = node['expr']
    .return tree.'get'('past', $P0, 'Lua::Grammar::expr')
}


transform past (Lua::Grammar::expr) :language('PIR') {
    .local string type
    type = node['type']
    unless type == 'term:' goto L1
    $S0 = node.'find_key'()
    $P0 = node[$S0]
    $S0 = concat 'Lua::Grammar::', $S0
    .return tree.'get'('past', $P0, $S0)
  L1:
    .local pmc optable, optok
    .local string pirop, returns
    optable = get_hll_global [ 'Lua::Grammar' ], '$optable'
    optok = optable[type]
    pirop = optok['pirop']
    .local pmc past
    past = new 'PAST::Op'
    if pirop == '' goto L2
    returns = optok['returns']
    past.'init'('node'=>node, 'name'=>type, 'pasttype'=>'pirop', 'pirop'=>pirop, 'returns'=>returns)
    goto L3
  L2:
    $S0 = optok['past']
    past.'init'('node'=>node, 'name'=>type, 'pasttype'=>$S0)
  L3:
    $P0 = node.'get_array'()
    .local pmc iter
    iter = new .Iterator, $P0
  L5:
    unless iter goto L4
    $P0 = shift iter
    $P1 = tree.'get'('past', $P0, 'Lua::Grammar::expr')
    past.'push'($P1)
    goto L5
  L4:
    .return (past)
}


transform past (Lua::Grammar::simple_expression) :language('PIR') {
    $S0 = node.'find_key'()
    $P0 = node[$S0]
    $S0 = concat 'Lua::Grammar::', $S0
    .return tree.'get'('past', $P0, $S0)
}


transform past (Lua::Grammar::primary_expression) :language('PIR') {
    # INCOMPLETE
    .local pmc past, iter, key
    $P0 = node['prefix_expression']
    past = tree.'get'('past', $P0, 'Lua::Grammar::prefix_expression')
    $P0 = node['name']
    if null $P0 goto L1
    iter = new .Iterator, $P0
  L2:
    unless iter goto L1
    $P0 = shift iter
    key = tree.'get'('key', $P0, 'Lua::Grammar::name')
    past = past.'new'('PAST::Var', past, key, 'node'=>node, 'scope'=>'keyed')
    goto L2
  L1:

    $P0 = node['index']
    if null $P0 goto L3
    iter = new .Iterator, $P0
  L4:
    unless iter goto L3
    $P0 = shift iter
    key = tree.'get'('key', $P0, 'Lua::Grammar::index')
    past = past.'new'('PAST::Var', past, key, 'node'=>node, 'scope'=>'keyed')
    goto L4
  L3:

    $P0 = node['function_args']
    unless null $P0 goto L9
    .return (past)

  L9:
    $P0 = shift $P0
    $P1 = tree.'get'('explist', $P0, 'Lua::Grammar::function_args')
    .return past.'new'('PAST::Op', past, $P1 :flat, 'node'=>node, 'pasttype'=>'call')
}


transform past (Lua::Grammar::prefix_expression) :language('PIR') {
    $S0 = node.'find_key'()
    $P0 = node[$S0]
    $S0 = concat 'Lua::Grammar::', $S0
    .return tree.'get'('past', $P0, $S0)
}


transform past (Lua::Grammar::name) :language('PIR') {
    .local pmc past
    .local pmc symbtab
    symbtab = get_hll_global ['Lua::PAST'], '$symbtab'
    $S0 = node.'result_object'()
    past = new 'PAST::Var'
    $I0 = symbtab.'lookup'($S0)
    unless $I0 goto L1
    past.'init'('node'=>node, 'name'=>$S0, 'scope'=>'lexical')
    .return (past)
  L1:
    past.'init'('node'=>node, 'name'=>$S0, 'scope'=>'package')
    .return (past)
}


transform past (Lua::Grammar::vararg) :language('PIR') {
    .local pmc has_vararg
    has_vararg = get_hll_global ['Lua::PAST'], '?vararg'
    unless has_vararg goto L1
    .local pmc past
    new past, 'PAST::Op'
    past.'init'('node'=>node, 'pasttype'=>'vararg')
    .return (past)
  L1:
    node.'syntax_error'("cannot use '...' outside a vararg function")
}


transform past (Lua::Grammar::nil) :language('PIR') {
    .local pmc past
    new past, 'PAST::Val'
    past.'init'('node'=>node, 'vtype'=>'.LuaNil')
    .return (past)
}


transform past (Lua::Grammar::true) :language('PIR') {
    .local pmc past
    past = new 'PAST::Val'
    past.'init'('node'=>node, 'vtype'=>'.LuaBoolean', 'name'=>1)
    .return (past)
}


transform past (Lua::Grammar::false) :language('PIR') {
    .local pmc past
    past = new 'PAST::Val'
    past.'init'('node'=>node, 'vtype'=>'.LuaBoolean', 'name'=>0)
    .return (past)
}


transform past (Lua::Grammar::number) :language('PIR') {
    .local pmc past
    past = new 'PAST::Val'
    $S0 = node.'result_object'()
    past.'init'('node'=>node, 'vtype'=>'.LuaNumber', 'name'=>$S0, 'ctype'=>'n+')
    .return (past)
}


transform past (Lua::Grammar::string) :language('PIR') {
    $S0 = node.'find_key'()
    $P0 = node[$S0]
    $S1 = $P0.'result_object'()
    .local pmc past
    past = new 'PAST::Val'
    past.'init'('node'=>node, 'vtype'=>'.LuaString', 'name'=>$S1, 'ctype'=>'s~')
    .return (past)
}

=head1 AUTHORS

Klaas-Jan Stol <parrotcode@gmail.com>

Francois Perrad

=cut

