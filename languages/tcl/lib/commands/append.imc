###
# [append]

#
# append var [...]

.namespace [ "Tcl" ]

.sub "append"
  .local pmc argv
  argv = foldup

  .local int argc 
  argc = argv

  .local int return_type

  .local pmc value
  .local int looper
 
  if argc == 0 goto error
  looper = 1

  .local pmc read
  read = find_global "_Tcl", "__read"
  $S1 = argv[0]
  (return_type, $P1) = read($S1)
  
  if return_type == TCL_ERROR goto new_variable
  value = clone $P1
  goto loop

new_variable:
  value = new TclString
  value = ""
  #goto loop

loop:
  if looper == argc goto loop_done

  $S2 = argv[looper]
  concat value, $S2
  inc looper
  goto loop

loop_done:
  .local pmc set
  set = find_global "_Tcl", "__set"
  (return_type, value) = set($S1, value)
  goto done

error:
  return_type = TCL_ERROR
  value = new TclString
  value = "wrong # args: should be \"append varName ?value value ...?\""

done:
  .return(return_type, value)
.end
