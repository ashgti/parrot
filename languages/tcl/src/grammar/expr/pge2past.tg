grammar TclExpr::PAST::Grammar is TGE::Grammar;

transform result (ROOT) {
    $P0 = node['expr']
    if $P0 goto expression
    .return tree.get('dispatch', node, 'TclExpr::Grammar')

expression:
    .return tree.get('expression', node)
} 

transform expression (TclExpr::Grammar) {
    node = node['expr']
    .return tree.get('dispatch', node, 'TclExpr::Grammar')
}

# Handle generic dispatch (needed at ROOT, and for things with kids.)

transform dispatch (TclExpr::Grammar) {
    .local pmc child

    # Ask the child node for its result.

    $P0 = node['constant']
    if $P0 goto constant
    $P0 = node['braced']
    if $P0 goto braced
    $P0 = node['integer']
    if $P0 goto integer
    $P0 = node['float']
    if $P0 goto has_float
    $P0 = node['scientific']
    if $P0 goto scientific
    $P0 = node['number']
    if $P0 goto number
    $P0 = node['quoted']
    if $P0 goto quoted
    $P0 = node['function']
    if $P0 goto function
    $P0 = node['subcommand']
    if $P0 goto subcommand
    $P0 = node['variable']
    if $P0 goto variable
    $P0 = node['type']
    if $P0 goto operator
  
  .return(node)
 
operator:
    .return tree.get('result', node, 'operator')

function:
    .return tree.get('result', $P0, 'function')

subcommand:
    .return tree.get('result', $P0, 'subcommand')

quoted:
    .return tree.get('result', $P0, 'quoted')

braced:
    .return tree.get('result', $P0, 'braced')

number:
    .return tree.get('result', $P0, 'number')

constant:
    .return tree.get('result', $P0, 'constant')

integer:
    .return tree.get('result', $P0, 'integer')

has_float:
    .return tree.get('result', $P0, 'float')

scientific:
    .return tree.get('result', $P0, 'scientific')

variable:
    .return tree.get('result', $P0, 'variable')
}

# Go through all the children of an operator and transform them.

transform result (operator) {
    # for each child of this operator, dispatch to get the result,
    # use those as our children.
    .local pmc children, iter, past
    .local string name

    name = node['type']
    if name == 'ternary:? :' goto is_ternary
    if name == 'infix:&&' goto is_and
    if name == 'infix:||' goto is_or

    past = new 'PAST::Op'

    past['opfunc'] = name
    $P0 = node.'get_array'()
    if null $P0 goto iter_end
    iter = new .Iterator, $P0
  iter_loop:
    unless iter goto iter_end
    $P0 = shift iter
    $S0 = $P0
    if $S0 == '' goto iter_loop # XXX needed?
    $P1 = tree.'get'('dispatch', $P0, 'TclExpr::Grammar')
    past.'add_child'($P1)
    goto iter_loop
  iter_end:
    past['children'] = 1
    .return (past)

  is_ternary:
    .return tree.get('ternary', node, 'PAST::Op')
  is_and:
    .return tree.get('logical_and', node, 'PAST::Op')
  is_or:
    .return tree.get('logical_or', node, 'PAST::Op')
}

transform result (function) {
    .local string function_name
    .local pmc args
    .local int argc
    args = node['expression']
    argc = 0
    unless args goto has_argc
    argc = elements args

has_argc:
    .local pmc past
    past = new 'PAST::Op'
    $P0  = node['unary_function']
    if $P0 goto unary
    $P0  = node['nullary_function']
    if $P0 goto nullary
    $P0  = node['binary_function']
    if $P0 goto binary
 
  # never get here

  nullary:
    if argc > 0 goto too_many
    function_name = node['nullary_function']
    goto arguments

  unary:
    if argc == 0 goto too_few
    if argc > 1  goto too_many
    function_name = node['unary_function']
    $P0 = args[0]
    $P1 = tree.'get'('expression', $P0)
    past.'add_child'($P1)
    goto arguments

  binary:
    if argc < 2 goto too_few
    if argc > 2 goto too_many
    function_name = node['binary_function']

    $P1 = args[0]
    $P2 = tree.'get'('expression', $P1)
    past.'add_child'($P2)

    $P1 = args[1]
    $P2 = tree.'get'('expression', $P1)
    past.'add_child'($P2)

  arguments:
    function_name = 'function:' . function_name
    past['opfunc'] = function_name

    past['children'] = 1
    .return (past)

too_few:
    .throw('too few arguments for math function')

too_many:
    .throw('too many arguments for math function')
}

transform result (constant) {
    .local pmc result
    result = new 'PAST::Val'
    result.set_node(node)
    
    $S0 = node
    result['value'] = $S0
    result['class'] = "TclConst"
    .return(result)
}

transform result (braced) {
    .local pmc result
    result = new 'PAST::Val'
    result.set_node(node)
    
    $S0 = node[0]
    result['value'] = $S0
    result['class'] = "TclConst"
    .return(result)
}

transform result (number) {
    $P0 = node['integer']
    if $P0 goto integer
    $P0 = node['float']
    if $P0 goto have_float
    $P0 = node['scientific']
    if $P0 goto scientific
    .throw("invalid number type")

integer:
    .return tree.get('result', $P0, 'integer')

have_float:
    .return tree.get('result', $P0, 'float')

scientific:
    .return tree.get('result', $P0, 'scientific')
}

transform result (scientific) {
    .local pmc result
    result = new 'PAST::Val'
    result.set_node(node)
    
    $S0 = node[0]
    $N0 = $S0
    $S0 = node[1]
    $N1 = $S0
    $N1 = 10**$N1
    $N0 *= $N1
    
    $S0 = $N0
    result['value'] = $S0
    result['class'] = "TclFloat"

    .return(result)
}

transform result (integer) {
    .local pmc result
    result = new 'PAST::Val'
    result.set_node(node)

    $P0 = node['octal']
    if $P0 goto octal
    $P0 = node['hex']
    if $P0 goto hex

  decimal:
    $S0 = node
    result['value'] = $S0
    result['class'] = "TclInt"
    .return (result)
 
  octal: 
     $S0 = node

    .local int octal_value
    .local int octal_digit
    .local int octal_counter
    .local int octal_length
    octal_length = length $S0
    octal_counter = 1 #skip the first 0
    dec octal_counter
    octal_value = 0
    # at this point, string should consist only of digits 0-7
  octal_loop:
    if octal_counter == octal_length goto octal_done # skip first 0
    octal_digit = ord $S0, octal_counter
    octal_digit -= 48 # ascii value of 0
    octal_value *= 8
    octal_value += octal_digit
    inc octal_counter
    goto octal_loop
  
  octal_done:
    result['value'] = octal_value
    result['class'] = "TclInt"
    .return (result)

  hex:
    $S0 = node
    .local int len, digit, hex_value
    len = length $S0
    $I0 = 2 # skip 0x
    hex_value = 0
  hex_loop:
    if $I0 == len goto hex_done
    digit = ord $S0, $I0
    # higher than Z - must be lowercase letter
    if digit > 90 goto hex_lower
    # higher than 9 - must be uppercase letter
    if digit > 57 goto hex_upper
    digit -= 48 # value of 0
    goto hex_next
  hex_lower:
    digit -= 97 # value of a
    digit += 10
    goto hex_next
  hex_upper:
    digit -= 65 # value of A
    digit += 10
    # goto hex_next
  hex_next:
    hex_value *= 16
    hex_value += digit
    inc $I0
    goto hex_loop

  hex_done:
    result['value'] = hex_value
    result['class'] = "TclInt"
    .return(result)

}

transform result (float) {
    .local pmc result
    result = new 'PAST::Val'
    result.set_node(node)

    $S0 = node
    result['value'] = $S0
    result['class'] = "TclFloat"
    .return (result)
}

transform result (quoted) {
    .local pmc past
    $P0 = node['quoted_literal']
    $P0 = $P0.'get_array'()
    if null $P0 goto empty_string
    
    .local pmc iter
    iter = new .Iterator, $P0
    null past
  iter_loop:
    unless iter goto iter_end
    .local pmc cnode, cpast
    cnode = shift iter
    $S0 = cnode['type']
    if $S0 == 'str' goto cpast_str
    cpast = tree.'get'('result', cnode, $S0)
    goto cpast_add
  cpast_str:
    $S0 = cnode
    cpast = new 'PAST::Val'
    cpast['node'] = node
    cpast['class'] = 'TclConst'
    cpast['value'] = $S0
  cpast_add:
    unless null past goto cpast_concat
    past = cpast
    goto iter_loop
  cpast_concat:
    $P0 = new 'PAST::Op'
    $P0.add_child(past)
    $P0.add_child(cpast)
    $P0['node'] = node
    $P0['opfunc'] = 'infix:concat'
    past = $P0
    goto iter_loop
  iter_end:
    .return(past)

empty_string:
    past = new 'PAST::Val'
    past['node']  = node
    past['class'] = 'TclConst'
    past['value'] = ''

    .return(past)
}

transform result (subcommand) {
    .local pmc past
    past = new 'PAST::Command'
    past.set_node(node)
    
    .local pmc words, iter
    words = node['command'; 0; 'word']
    iter  = new .Iterator, words
loop:
    unless iter goto end
    $P0 = shift iter
    $P1 = tree.'get'('dispatch', $P0, 'TclExpr::Grammar')
    past.'add_child'($P1)
    goto loop
end:
    .return (past)
}

transform result (variable) {
    .local pmc result
    result = new 'PAST::Var'
    result.set_node(node)
    
    $S0 = node[0]
    result['name'] = $S0
    .return(result)
}

transform ternary (PAST::Op) {
    .local pmc result

    result = new 'PAST::Cond'
    result.set_node(node)

    $P1 = node[0]
    $P2 = tree.'get'('dispatch', $P1, 'TclExpr::Grammar')
    result.'add_child'($P2)
    $P1 = node[1]
    $P2 = tree.'get'('dispatch', $P1, 'TclExpr::Grammar')
    result.'add_child'($P2)
    $P1 = node[2]
    $P2 = tree.'get'('dispatch', $P1, 'TclExpr::Grammar')
    result.'add_child'($P2)
 
    result['conditional'] = 1
 
    .return(result)
}

transform logical_and (PAST::Op) {
    .local pmc result, true, false, A, B
    .local pmc inner

    # Transform A && B into:
    # A ? ( B ? 1 : 0) : 0

    true = new 'PAST::Val'
    true['value'] = 1
    true['class'] = "TclInt"

    false = new 'PAST::Val'
    false['value'] = 0
    false['class'] = "TclInt"

    # Get our A & B trees..
    $P1 = node[0]
    A = tree.'get'('dispatch', $P1, 'TclExpr::Grammar')
    $P1 = node[1]
    B = tree.'get'('dispatch', $P1, 'TclExpr::Grammar')

    # Generate (B ? 1 : 0)
    inner = new 'PAST::Cond'
    inner.'add_child'(B)

    inner.'add_child'(true)
    inner.'add_child'(false)
    inner['conditional'] = 1

    # Generate (A ? (...) : 0)
    result = new 'PAST::Cond'
    result.set_node(node)

    result.'add_child' (A)

    result.'add_child' (inner)
    result.'add_child' (false)
 
    result['conditional'] = 1
 
    .return(result)
}

transform logical_or (PAST::Op) {
    .local pmc result, true, false, A, B
    .local pmc inner

    # Transform A || B into:
    # A ? 1 : ( B ? 1 : 0)

    true = new 'PAST::Val'
    true['value'] = 1
    true['class'] = "TclInt"

    false = new 'PAST::Val'
    false['value'] = 0
    false['class'] = "TclInt"

    # Get our A & B trees..
    $P1 = node[0]
    A = tree.'get'('dispatch', $P1, 'TclExpr::Grammar')
    $P1 = node[1]
    B = tree.'get'('dispatch', $P1, 'TclExpr::Grammar')

    # Generate (B ? 1 : 0)
    inner = new 'PAST::Cond'
    inner.'add_child'(B)

    inner.'add_child'(true)
    inner.'add_child'(false)
    inner['conditional'] = 1

    # Generate (A ? 1 : (...))
    result = new 'PAST::Cond'
    result.set_node(node)

    result.'add_child' (A)

    result.'add_child' (true)
    result.'add_child' (inner)
 
    result['conditional'] = 1
 
    .return(result)
}
