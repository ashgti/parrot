# $Id$

=begin overview

This is the grammar for lolcode written as a sequence of Perl 6 rules.

=end overview

grammar lolcode::Grammar is PCT::Grammar;

rule TOP {
    'HAI' <float> <statement_terminator>
    <block>
    'KTHXBYE' <statement_terminator>
    [ $ || <panic: Syntax error> ]
    {*}
}

rule statement {
    | <visible>    {*}   #= visible
    | <declare>    {*}   #= declare
    | <assign>     {*}   #= assign
    | <function>   {*}   #= function
    | <expression> {*}   #= bare_expression
}

token statement_terminator { [ ',' | \n+ | $ ] }

rule visible {
    'VISIBLE' <expression> [ <expression> ]* [$<no_newline>='!']?
    {*}
}

rule declare {
    'I' 'HAS' 'A' <variable> [ 'ITZ' <expression> ]?
    {*}
}

rule assign {
    <variable> 'R' <expression> {*}
}

rule function {
    'HOW' 'DUZ' 'I' <variable> <statement_terminator>
    <block>
    'IF' 'U' 'SAY' 'SO'
    {*}
}

rule ifthen {
    <expression> <statement_terminator>
    'O' 'RLY?' <statement_terminator>
    'YA' 'RLY' <statement_terminator>
    <block>
    [
      'MEBBE' <expression> <statement_terminator>
      <block>
    ]*
    [
      'NO' 'WAI' <statement_terminator>
      <block>
    ]?
    'OIC'
    {*}
}

rule switch {
    <expression> <statement_terminator>
    'WTF?' <statement_terminator>
    [
      'OMG' <value> <statement_terminator>
      <block>
    ]+
    [
      'OMGWTF' <statement_terminator>
      <block>
    ]?
    'OIC'
}

rule block {
    [<statement> <statement_terminator>]* {*}
}

rule parameters {
    'YR' <identifier> [ 'AN' 'YR' <identifier> ]*
}

rule expression {
    | <variable> {*}                             #= var_or_function
    | <value> {*}                                #= value
}

rule value {
    | <integer>  {*}                             #= integer
    | <quote>    {*}                             #= quote
}

rule variable { <identifier> {*} }

token identifier { <!keyword> $<name>=( <[a..zA..Z]> \w* ) {*} }

token keyword {
    [ 'A' | 'AN' | 'FAIL' | 'FOUND' | 'GTFO' | 'HAS' | 'HOW' | 'I' | 'IF'
    | 'KTHXBYE' | 'MEBBE' | 'NO' | 'O' | 'OIC' | 'OMG' | 'OMGWTF' | 'R'
    | 'RLY' | 'RLY?' | 'SAY' | 'SO' | 'U' | 'VISIBLE' | 'WAI' | 'WIN' | 'WTF?' | 'YA'
    | 'YR' ] >>
}

rule integer { \d+ {*} }

token float { \d+ [ '.' \d+ ]? }

rule quote {
    [ \" <string_literal: "> \" ]
    {*}
}

token ws { <!ww> [
    | ^^ \h* BTW \h \N* \n+
    | ^^ \h* OBTW .*? ^^ \h* TLDR \n+
    | \h+
    ]*
}
