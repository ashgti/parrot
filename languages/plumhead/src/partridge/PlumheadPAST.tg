# $Id$

grammar Plumhead::PAST::Grammar is TGE::Grammar;

# Generate PAST-pm from Plumhead parse tree

transform past (ROOT) :language('PIR') {

    node = node['program']
    .local pmc past
    past = new 'PAST::Block'
    past.'init'( 'node' => node, 'name'=>'plumhead' )
    
    .local pmc cpast
    cpast = tree.'get'('past', node, 'Plumhead::Grammar::program')
    past.'push'(cpast)

    .return (past)
}

transform past (Plumhead::Grammar::program) :language('PIR') {

    .local pmc past
    past = new 'PAST::Stmts'
    past.'init'( 'node' => node )

    $P0 = node['SEA']
    if null $P0 goto handled_sea
    .local pmc past_sea
    past_sea = tree.'get'('past', $P0, 'Plumhead::Grammar::SEA')
    past.'push'(past_sea)
handled_sea:

    $P0 = node['code']
    if null $P0 goto handled_code
    .local pmc past_code
    past_code = tree.'get'('past', $P0, 'Plumhead::Grammar::code')
    past.'push'(past_code)
handled_code:

    .return (past)
}

transform past (Plumhead::Grammar::SEA) :language('PIR') {

    .local string val
    val = node
    .local pmc code_string
    code_string = new 'PGE::CodeString'
    ( val ) = code_string.'escape'( val )

    .local pmc past_val
    past_val = new 'PAST::Val'                             
    past_val.'init'( 'name' => val, 'vtype' => '.Undef' )                              

    .local pmc past                                                  
    past = new 'PAST::Op'
    past.'init'( past_val, 'node'=> node, 'pirop' => 'print' )  

    .return (past)
}

transform past (Plumhead::Grammar::code) :language('PIR') {

    .local pmc past
    past = new 'PAST::Stmts'
    past.'init'( 'node' => node )

    $P0 = node['statement']
    if null $P0 goto handled_statement
    .local pmc iter
    iter = new .Iterator, $P0
  iter_loop:
    unless iter goto iter_end
    .local pmc cnode, cpast
    cnode = shift iter
    if null cnode goto iter_loop
    $P1 = cnode['ECHO']
    if null $P1 goto iter_loop
    $P2 = cnode['expression']
    if null $P2 goto iter_loop
    cpast = tree.'get'('past', $P2, 'Plumhead::Grammar::expression')
    if null cpast goto iter_loop
    past.'push'(cpast)
    goto iter_loop
  iter_end:

    handled_statement:

    .return (past)
}

transform past (Plumhead::Grammar::expression) :language('PIR') {

    .local pmc past

    $P0 = node['DOUBLEQUOTE_STRING']
    if null $P0 goto no_DOUBLEQUOTE_STRING
    past = tree.'get'('past', $P0, 'Plumhead::Grammar::DOUBLEQUOTE_STRING')
    goto handled_expression
    no_DOUBLEQUOTE_STRING:

    $P0 = node['SINGLEQUOTE_STRING']
    if null $P0 goto no_SINGLEQUOTE_STRING
    past = tree.'get'('past', $P0, 'Plumhead::Grammar::SINGLEQUOTE_STRING')
    goto handled_expression
    no_SINGLEQUOTE_STRING:

    $P0 = node['NUMBER']
    if null $P0 goto no_NUMBER
    past = tree.'get'('past', $P0, 'Plumhead::Grammar::NUMBER')
    goto handled_expression
    no_NUMBER:

    $P0 = node['adding_expression']
    if null $P0 goto no_adding_expression
    past = tree.'get'('past', $P0, 'Plumhead::Grammar::expression')
    goto handled_expression
    no_adding_expression:

    $P0 = node['multiplying_expression']
    if null $P0 goto no_multiplying_expression
    past = tree.'get'('past', $P0, 'Plumhead::Grammar::expression')
    goto handled_expression
    no_multiplying_expression:

    $P0 = node['unary_expression']
    if null $P0 goto no_unary_expression
    past = tree.'get'('past', $P0, 'Plumhead::Grammar::expression')
    goto handled_expression
    no_unary_expression:

    $P0 = node['postfix_expression']
    if null $P0 goto no_postfix_expression
    past = tree.'get'('past', $P0, 'Plumhead::Grammar::expression')
    goto handled_expression
    no_postfix_expression:

    handled_expression:

    .return (past)
}


transform past (Plumhead::Grammar::DOUBLEQUOTE_STRING) :language('PIR') {

    .local pmc past_val
    past_val = new 'PAST::Val'                             
    .local string val
    val = node
    .local pmc code_string
    past_val.'init'( 'name' => val, 'vtype' => '.Undef' )                              

    .local pmc past                                                  
    past = new 'PAST::Op'
    past.'init'( past_val, 'node'=> node, 'pirop' => 'print' )  

    .return (past)
}


transform past (Plumhead::Grammar::SINGLEQUOTE_STRING) :language('PIR') {

    .local pmc past_val
    past_val = new 'PAST::Val'                             
    .local string val
    val = node
    .local pmc code_string
    past_val.'init'( 'name' => val, 'vtype' => '.Undef' )                              

    .local pmc past                                                  
    past = new 'PAST::Op'
    past.'init'( past_val, 'node'=> node, 'pirop' => 'print' )  

    .return (past)
}

transform past (Plumhead::Grammar::NUMBER) :language('PIR') {

    .local pmc past_val
    past_val = new 'PAST::Val'                             
    .local string val
    val = node
    past_val.'init'( 'name' => val, 'vtype' => '.Float', 'ctype' => 'n+' )                              

    .local pmc past                                                  
    past = new 'PAST::Op'
    past.'init'( past_val, 'node'=> node, 'name' => 'print' )  

    .return (past)
}

