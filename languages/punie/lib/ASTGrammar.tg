=head1 NAME

ASTGrammar -- A grammar for transforming the output of a PGE parse into
an abstract syntax tree.

=head1 SYNOPSYS

  .sub _main :main
      load_bytecode 'ASTGrammar.pir'
      .local pmc grammar
       grammar = new 'ASTGrammar'

      # Construct the "AST"
      load_bytecode "PAST.pbc"
      .local pmc astbuilder
      astbuilder = grammar.apply(match)
      .local pmc ast
      ast = astbuilder.get('result')

      end
  .end

=head1 DESCRIPTION


=cut

grammar ASTGrammar is TGE::Grammar;

transform result (ROOT) :language('PIR') {
    # Ask the child node for its result
    .local pmc child
    $I0 = defined node["PunieGrammar::lineseq"]
    unless $I0 goto err_no_tree
    $P0 = node["PunieGrammar::lineseq"]
    child = tree.get('result', $P0, 'PunieGrammar::lineseq')

    .return (child)

  err_no_tree:
    print "The top-level node doesn't contain an 'lineseq' match.\n"
    end
}

transform result (PunieGrammar::block) :language('PIR') {
    # Ask the child node for its result
    .local pmc child
    $I0 = defined node["PunieGrammar::lineseq"]
    unless $I0 goto err_no_tree
    $P0 = node["PunieGrammar::lineseq"]
    child = tree.get('result', $P0, 'PunieGrammar::lineseq')

    .return (child)

  err_no_tree:
    print "The block node doesn't contain a 'lineseq' match.\n"
    end
}

transform result (PunieGrammar::lineseq) :language('PIR') {
    .local pmc result
    result = new 'PAST::Stmts'
    result.'clone'(node)

    # Ask the child node for its result
    .local pmc child
    $I0 = defined node["PunieGrammar::line"]
    unless $I0 goto err_no_tree
    $P0 = node["PunieGrammar::line"]

    .local pmc iter
    iter = new Iterator, $P0    # setup iterator for node
    iter = 0
  iter_loop:
    unless iter, iter_end         # while (entries) ...
      shift $P2, iter
      $P3 = tree.get('result', $P2, 'PunieGrammar::line')
      result.'add_child'($P3)
      goto iter_loop
  iter_end:

    .return (result)
  err_no_tree:
    print "The 'lineseq' node doesn't contain a 'line' match.\n"
    end
}

transform result (PunieGrammar::line) :language('PIR') {
    .local pmc result
    result = new 'PAST::Stmt'
    result.'clone'(node)

    .local pmc iter
    iter = new Iterator, node    # setup iterator for node
    iter = 0
  iter_loop:
    unless iter, iter_end         # while (entries) ...
      shift $S1, iter           # get the key of the iterator
      $P2 = iter[$S1]
      $P3 = tree.get('result', $P2, $S1)
      if null $P3 goto iter_loop
      result.'add_child'($P3)
      goto iter_loop
  iter_end:

    .return (result)
}

transform result (PunieGrammar::expr) :language('PIR') {
    .local pmc result
    result = new 'PAST::Exp'
    result.'clone'(node)

    $P1 = node.get_hash()
    $P0 = new Iterator, $P1    # setup iterator for node
    set $P0, 0 # reset iterator, begin at start
  iter_loop:
    unless $P0, iter_end         # while (entries) ...
      shift $S2, $P0             # get key for next entry
      $P2 = $P0[$S2]      # get entry at current key
      $P3 = tree.get('result', $P2, $S2)
      if null $P3 goto iter_loop
      result.'add_child'($P3)
      goto iter_loop
  iter_end:

    .return (result)
}

transform result (PunieGrammar::gprint) :language('PIR') {
    .local pmc result
    result = new 'PAST::Op'
    result.'clone'(node)
    $S1 = node[0]
    result.'op'($S1)

    $P1 = node.get_hash()
    $P0 = new Iterator, $P1    # setup iterator for node
    set $P0, 0 # reset iterator, begin at start
  iter_loop:
    unless $P0, iter_end         # while (entries) ...
      shift $S2, $P0             # get key for next entry
      $P2 = $P0[$S2]      # get entry at current key
      $P3 = tree.get('result', $P2, $S2)
      if null $P3 goto iter_loop
      result.'add_child'($P3)
      goto iter_loop
  iter_end:

    .return (result)
}

transform result (PunieGrammar::cond) :language('PIR') {
    .local pmc result
    result = new 'PAST::Op'
    result.'clone'(node)
    $S1 = node[0]
    result.'op'($S1)

    $P1 = node.get_hash()
    .local pmc iter
    iter = new Iterator, $P1    # setup iterator for node
    set iter, 0 # reset iterator, begin at start
  iter_loop:
    unless iter, iter_end         # while (entries) ...
      shift $S2, iter             # get key for next entry
      $P2 = iter[$S2]      # get entry at current key
      $P3 = tree.get('result', $P2, $S2)
      if null $P3 goto iter_loop
      result.'add_child'($P3)
      goto iter_loop
  iter_end:

    .return (result)
}

transform result (PunieGrammar::else) :language('PIR') {
    .local pmc onechild
    onechild = node[0]
    unless onechild goto no_child
    .local pmc result
    result = new 'PAST::Op'
    result.'clone'(onechild)
    $S1 = onechild[0]
    result.'op'($S1)

    $P1 = onechild.get_hash()
    .local pmc iter
    iter = new Iterator, $P1    # setup iterator for node
    set iter, 0 # reset iterator, begin at start
  iter_loop:
    unless iter, iter_end         # while (entries) ...
      shift $S2, iter             # get key for next entry
      $P2 = iter[$S2]      # get entry at current key
      $P3 = tree.get('result', $P2, $S2)
      if null $P3 goto iter_loop
      result.'add_child'($P3)
      goto iter_loop
  iter_end:

    .return (result)
  no_child:
    .return ()
}

transform result (PunieGrammar::label) :language('PIR') {
    .return ()
}

transform result (PunieGrammar::cexpr) :language('PIR') {
    .local pmc result
    $I0 = defined node["PunieGrammar::oexpr"]
    unless $I0 goto err_no_oexpr
    $P1 = node["PunieGrammar::oexpr"]
    result = new 'PAST::Op'
    result.'clone'(node)
    result.'op'('O_COMMA')

    $P0 = new Iterator, $P1    # setup iterator for node
    set $P0, 0 # reset iterator, begin at start
  iter_loop:
    unless $P0, iter_end         # while (entries) ...
      shift $P2, $P0             # get next entry
      $P3 = tree.get('result', $P2, 'PunieGrammar::oexpr')
      if null $P3 goto iter_loop
      result.'add_child'($P3)
      goto iter_loop
  iter_end:

    # If there's only one child node, it's a single element, not a list,
    # so just return the single element. Otherwise, we have a comma
    # separated list, so build a comma op node.
    .local pmc children
    children = result.'children'()
    $I0 = elements children
    unless $I0 > 1 goto no_comma
    .return (result)
  no_comma:
    result = shift children # there's only one result
    .return (result)
  err_no_oexpr:
    print "The cexpr node doesn't contain a 'oexpr' match.\n"
    end
}

transform result (PunieGrammar::oexpr) :language('PIR') {
    .local pmc result
    result = new 'PAST::Exp'
    result.'clone'(node)

    .local pmc iter
    $P1 = node.get_hash()
    iter = new Iterator, $P1    # setup iterator for node
    iter = 0
  iter_loop:
    unless iter, iter_end         # while (entries) ...
      shift $S1, iter           # get the key of the iterator
      $P2 = iter[$S1]
      $P3 = tree.get('result', $P2, $S1)
      if null $P3 goto iter_loop
      result.'add_child'($P3)
      goto iter_loop
  iter_end:

    .return (result)
}


transform result (PunieGrammar::variable) :language('PIR') {
    .local pmc result
    result = new 'PAST::Var'
    result.'clone'(node)
    .local string sigil
    .local string name
    sigil = node['sigil']
    $S1 = node['word']
    name = sigil . $S1

    result.'varname'(name)
    result.'scope'('global')

    unless sigil == '$' goto not_scalar
      result.'vartype'('scalar')
  not_scalar:

    .return (result)
}

transform result (PunieGrammar::number) :language('PIR') {
    .local pmc result
    result = new 'PAST::Val'
    result.'clone'(node)
    $S2 = node 
    result.'value'($S2)
    result.'valtype'('num')

    .return (result)
}

transform result (PunieGrammar::integer) :language('PIR') {
    .local pmc result
    result = new 'PAST::Val'
    result.'clone'(node)
    $S2 = node 
    result.'value'($S2)
    result.'valtype'('int')
    .return (result)
}

transform result (PunieGrammar::stringdouble) :language('PIR') {
    .local pmc result
    result = new 'PAST::Val'
    result.'clone'(node)

    .local string value
    # Check if this is a string match
    $I0 = defined node["PGE::Text::bracketed"]
    if $I0 goto bracketed_value
    value = node
    goto no_bracketed_value
  bracketed_value:
    $P1 = node["PGE::Text::bracketed"]
    $P2 = $P1[0]
    value = $P2
  no_bracketed_value:

    result.'value'(value)
    result.'valtype'('strqq')
    .return (result)
}

transform result (PunieGrammar::stringsingle) :language('PIR') {
    .local pmc result
    result = new 'PAST::Val'
    result.'clone'(node)

    .local string value
    # Check if this is a string match
    $I0 = defined node["PGE::Text::bracketed"]
    if $I0 goto bracketed_value
    value = node
    goto no_bracketed_value
  bracketed_value:
    $P1 = node["PGE::Text::bracketed"]
    $P2 = $P1[0]
    value = $P2
  no_bracketed_value:

    result.'value'(value)
    result.'valtype'('strq')
    .return (result)
}

# The following rules are for the results of the operator precedence
# parser. These operate very differently than the standard grammar
# rules, because they give the node type in a "type" hash key inside the
# node, instead of storing the node as the value of a hash key that
# is their type.

transform result (expr) :language('PIR') {
    .local string type
    type = node["type"]
    unless node goto error_no_node
    if type == 'term:' goto transform_term
      # else
      $P1 = tree.get('op', node, 'expr')
      .return($P1)
    transform_term:
      $P1 = tree.get('term', node, 'expr')
      .return($P1)
    error_no_node:
      print "error: no node\n"
      end
}

transform op (expr) :language('PIR') {
    .local pmc result
    result = new 'PAST::Op'
    result.'clone'(node)
    $S1 = node["type"]
    result.'op'($S1)

    $P1 = node.get_array()
    .local pmc iter
    iter = new Iterator, $P1    # setup iterator for node
    set iter, 0 # reset iterator, begin at start
  iter_loop:
    unless iter, iter_end         # while (entries) ...
      shift $P2, iter            # get entry
      $P3 = tree.get('result', $P2, 'expr')
      if null $P3 goto iter_loop
      result.'add_child'($P3)
      goto iter_loop
  iter_end:

    .return (result)
}

transform term (expr) :language('PIR') {
    .local pmc result
    .local pmc children
    children = new .ResizablePMCArray
    $P1 = node.get_hash()
    $P0 = new Iterator, $P1    # setup iterator for node
    set $P0, 0 # reset iterator, begin at start
  iter_loop:
    unless $P0, iter_end         # while (entries) ...
      shift $S2, $P0             # get key for next entry
      # skip 'type' keys added by the operator precedence parser
      if $S2 == 'type' goto iter_loop 
      $P2 = $P0[$S2]      # get entry at current key
      $P3 = tree.get('result', $P2, $S2)
      if null $P3 goto iter_loop
      push children, $P3
      goto iter_loop
  iter_end:

    $I0 = elements children
    unless $I0 == 1 goto err_too_many
    result = children[0]
    .return (result)

  err_too_many:
    print "error: Currently, 'term' nodes should have only one child.\n"
    end
}

=head1 LICENSE

Copyright (C) 2006, The Perl Foundation.

This is free software; you may redistribute it and/or modify
it under the same terms as Parrot.

=head1 AUTHOR

Allison Randal <allison@perl.org>

=cut
