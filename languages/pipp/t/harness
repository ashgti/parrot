#! perl
# $Id$

=head1 NAME

languages/pipp/t/harness - A harness for Pipp

=head1 SYNOPSIS

  cd languages && perl -I../lib pipp/t/harness --files

  cd languages && perl -I../lib pipp/t/harness 

  cd languages && perl -I../lib pipp/t/harness --with-php

  cd languages && perl -I../lib pipp/t/harness --with-phc

  cd languages && perl -I../lib pipp/t/harness --with-antlr3

  cd languages && perl -I../lib pipp/t/harness --with-pct

  cd languages && perl -I../lib pipp/t/harness \
                   pipp/t/hello.t 

=head1 DESCRIPTION

If I'm called with a single
argument of "--files", I just return a list of files to process.
This list is one per line, and is relative to the languages dir.

If I'm called with no args, I run the complete suite.

Otherwise I run the tests that were passed on the command line.

=cut

# pragmata
use strict;
use warnings;
use lib '..';

use Cwd           ();
use File::Spec    ();
use TAP::Harness  3.0;
use Parrot::Config;

my $language = 'pipp';

if ( grep { m/^--files$/ } @ARGV ) {
    # Only the Makefile in 'parrot/languages' uses --files
    my $dir = File::Spec->catfile( $language, 't' );
    my @files = glob( File::Spec->catfile( $dir, '*/*.t' ) );
    print join( "\n", @files );
    print "\n" if scalar(@files);
} else { 
    my @files;
    # TODO: use Getopt::Long or such
    my $with_php = ( grep { m/^--with-php$/ } @ARGV ) ? 1 : 0;
    @ARGV = grep { ! m/^--with-php$/ } @ARGV;

    my $with_phc = ( grep { m/^--with-phc$/ } @ARGV ) ? 1 : 0;
    @ARGV = grep { ! m/^--with-phc$/ } @ARGV;

    my $with_antlr3 = ( grep { m/^--with-antlr3$/ } @ARGV ) ? 1 : 0;
    @ARGV = grep { ! m/^--with-antlr3$/ } @ARGV;

    my $with_pct = ( grep { m/^--with-pct$/ } @ARGV ) ? 1 : 0;
    @ARGV = grep { ! m/^--with-pct$/ } @ARGV;

    if ( scalar(@ARGV) ) {
        # Someone specified tests for me to run.
        @files = grep { -f $_ } @ARGV
    } else {
        ( undef, undef, my $current_dir ) = File::Spec->splitpath( Cwd::getcwd() );
        if ( $current_dir eq 'languages' ) {
            @files = glob( File::Spec->catfile( $language, 't', '*/*.t' ) );
        }
        elsif ( $current_dir eq $language ) {
            @files = glob( File::Spec->catfile( 't', '*/*.t' ) );
        }
        else {
            die "Where am I?";
        }
    }

    # XXX There should be a better to indicate which implementation should be used
    if ( $with_php ) {
       $ENV{PARROT_PIPP_TEST_MODULE} = 'Parrot::Test::Pipp::PHP';
    }
    elsif ( $with_phc ) {
       $ENV{PARROT_PIPP_TEST_MODULE} = 'Parrot::Test::Pipp::Phc';
    }
    elsif ( $with_antlr3 ) {
       $ENV{PARROT_PIPP_TEST_MODULE} = 'Parrot::Test::Pipp::Antlr3';
    }
    elsif ( $with_pct ) {
       $ENV{PARROT_PIPP_TEST_MODULE} = 'Parrot::Test::Pipp::PCT';
    }
    else {
       $ENV{PARROT_PIPP_TEST_MODULE} = 'Parrot::Test::Pipp::PCT';
    }

    my $harness = TAP::Harness->new( {
                      exec => sub {
                                       my ( $harness, $test_file ) = @_;
                                       
                                       # the directory t/pmc contains only PIR test files
                                       return [ "../parrot$PConfig{exe}",
                                                $test_file
                                              ] if $test_file =~ m!t/pmc/.*[.]t$!;

                                       return [ "../parrot$PConfig{exe}",
                                                'pipp/pipp.pbc',
                                                $test_file
                                              ] if $test_file =~ m!t/in_php/.*[.]t$!;

                                       return undef;
                                  }
                  } ); 
    $harness->runtests(@files);
}

=head1 HISTORY

Mostly taken from bc/t/harness.

=head1 SEE ALSO

  F<languages/perl6/t/harness>

=head1 AUTHOR

Bernhard Schmalhofer - <Bernhard.Schmalhofer@gmx.de>

=cut

# Local Variables:
#   mode: cperl
#   cperl-indent-level: 4
#   fill-column: 100
# End:
# vim: expandtab shiftwidth=4:
