# $Id$

=head1 NAME

languages/bc/t/harness - A harness for Parrot bc

=head1 SYNOPSIS

  cd languages && perl -I../lib bc/t/harness --files

  cd languages && perl -I../lib bc/t/harness 

  cd languages && perl -I../lib bc/t/harness --with-posix

  cd languages && perl -I../lib bc/t/harness --with-python

  cd languages && perl -I../lib bc/t/harness --with-java

  cd languages && perl -I../lib bc/t/harness \
                   bc/t/basic/integer.t 

=head1 DESCRIPTION

If I'm called with a single
argument of "--files", I just return a list of files to process.
This list is one per line, and is relative to the languages dir.

If I'm called with no args, I run the complete suite.

Otherwise I run the tests that were passed on the command line.

=cut

use strict;
use lib '..';

use Cwd();
use Data::Dumper;
use File::Spec;
use Test::Harness();

my $language = 'bc';

if ( grep { m/^--files$/ } @ARGV ) {
    # Only the Makefile in 'parrot/languages' uses --files
    my $dir = File::Spec->catfile( $language, 't' );
    my @files = glob( File::Spec->catfile( $dir, '*.t' ) );
    print join( "\n", @files );
    print "\n" if scalar(@files);
} else { 
    my @files;
    # TODO: use Getopt::Long or such
    my $with_posix = ( grep { m/^--with-posix$/ } @ARGV ) ? 1 : 0;
    @ARGV = grep { ! m/^--with-posix$/ } @ARGV;

    my $with_python = ( grep { m/^--with-python$/ } @ARGV ) ? 1 : 0;
    @ARGV = grep { ! m/^--with-python$/ } @ARGV;

    my $with_java = ( grep { m/^--with-java$/ } @ARGV ) ? 1 : 0;
    @ARGV = grep { ! m/^--with-java$/ } @ARGV;

    if ( scalar(@ARGV) ) {
        # Someone specified tests for me to run.
        @files = grep { -f $_ } @ARGV
    } else {
        ( undef, undef, my $current_dir ) = File::Spec->splitpath( Cwd::getcwd() );
        if ( $current_dir eq 'languages' ) {
            @files = glob( File::Spec->catfile( $language, 't', '*.t' ) );
        }
        elsif ( $current_dir eq $language ) {
            @files = glob( File::Spec->catfile( 't', '*.t' ) );
        }
        else {
            die "Where am I?";
        }
    }

    # XXX There should be a better to indicate which implementation should be used
    if ( $with_posix ) {
       $ENV{'PARROT_BC_TEST_MODULE'} = 'Parrot::Test::Bc::Posix';
    }
    elsif ( $with_java ) {
       $ENV{'PARROT_BC_TEST_MODULE'} = 'Parrot::Test::Bc::Antlr3';
    }
    elsif ( $with_python ) {
       $ENV{'PARROT_BC_TEST_MODULE'} = 'Parrot::Test::Bc::Antlr2';
    }
    else {
       $ENV{'PARROT_BC_TEST_MODULE'} = 'Parrot::Test::Bc::Antlr2';
    }

    Test::Harness::runtests( @files ) if scalar( @files );
}

=head1 HISTORY

Mostly taken from m4/t/harness.

=head1 SEE ALSO

  F<languages/tcl/t/harness>, F<languages/scheme/t/harness>, F<languages/m4/t/harness>, F<languages/python/t/harness>

=head1 AUTHOR

Bernhard Schmalhofer - <Bernhard.Schmalhofer@gmx.de>

=cut
