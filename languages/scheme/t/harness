#! perl -w
# $Id$

=head1 NAME

scheme/t/harness - a harness for scheme

=head1 SYNOPSIS

  cd languages && perl -I../lib scheme/t/harness -files

  cd languages && perl -I../lib scheme/t/harness 

  cd languages && perl -I../lib scheme/t/harness \
                   scheme/t/logic/basic.t \
                   scheme/t/logic/defines.t

=head1 DESCRIPTION

If I'm called with a single
argument of "-files", I just return a list of files to process.
This list is one per line, and is relative to the languages dir.

If I'm called with no args, I run the complete suite.

Otherwise I run the tests that were passed on the command line.

=cut

use strict;
use lib '../lib', 'scheme';

use Cwd();
use File::Spec;
use Test::Harness();

my $language = 'scheme';

if ( grep { m/^-files$/ } @ARGV ) {
    # Only the Makefile in 'parrot/languages' uses -file
    my $dir = File::Spec->catfile( $language, 't' );
    my @files = glob( File::Spec->catfile( $dir, '*/*.t' ) );
    print join( "\n", @files );
    print "\n" if scalar(@files);
} else { 
    my @files;
    if ( scalar(@ARGV) ) {
        # Someone specified tests for me to run.
        @files = grep { -f $_ } @ARGV
    } else {
        ( undef, undef, my $current_dir ) = File::Spec->splitpath( Cwd::getcwd() );
        if ( $current_dir eq 'languages' ) {
            @files = glob( File::Spec->catfile( $language, 't', '*', '*.t' ) );
        }
        elsif ( $current_dir eq $language ) {
            @files = glob( File::Spec->catfile( 't', '*', '*.t' ) );
        }
    }
    Test::Harness::runtests( @files ) if scalar( @files );
}

=head1 SEE ALSO

  L<languages/m4/t/harness>

=cut
