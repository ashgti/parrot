=head1 TITLE

Data::Dumper::Base - style baseclass

=head1 VERSION

version 0.10

=head1 SYNOPSIS

TDB

=head1 DESCRIPTION

This module provides the default output style of C<Data::Dumper>.

=head1 METHODS

A Data::Dumper::Base object has the following methods:

=over 4

=cut

.include "library/onload.imc"
.include "library/objects.imc"

.sub __onload
    __new_class( "Data::Dumper::Base" )
    .pcc_begin_return
    .pcc_end_return
.end

.namespace ["Data::Dumper::Base"]

=item style."prepare"( dumper, indent )

=cut

.sub prepare method
    .param pmc dumper
    .param string indent
    .local string stemp
    .local pmc temp

    setprop self, "dumper", dumper

    new temp, .PerlInt
    set temp, 0
    setprop self, "level", temp
    
    new temp, .PerlString
    clone stemp, indent
    set temp, stemp
    setprop self, "indention", temp

    new temp, .PerlArray
    setprop self, "cache", temp

    .pcc_begin_return
    .pcc_end_return
.end

=item (pos, name) = style."cache"( find, defname ) B<(internal)>

=cut

.sub cache method
    .param pmc find
    .param string defname
    .local pmc cache
    .local int i
    .local pmc entry
    .local string name
    .local pmc pname

    getprop cache, "cache", self
    set i, cache
LOOP:
    dec i
    dec i
    if i < 0 goto NOTFOUND
    entry = cache[i]
    ne_addr entry, find, LOOP

    # found entry => get its name
    inc i
    name = cache[i]
    dec i
    .pcc_begin_return
    .return i
    .return name
    .pcc_end_return

NOTFOUND:
    set name, defname
    push cache, find
    push cache, name
    .pcc_begin_return
    .return -1
    .return name
    .pcc_end_return
.end

=item indent = style."createIndent"() B<(internal)>

=cut

.sub createIndent method
    .param int level
    .local pmc temp
    .local string indent

    getprop temp, "indention", self
    set indent, temp
    clone indent, indent
    repeat indent, indent, level
    
    .pcc_begin_return
    .return indent
    .pcc_end_return
.end

=item indent = style."indent"()

=cut

.sub indent method
    .local pmc temp
    .local string indent
    .local int level
        
    getprop temp, "level", self
    set level, temp

    indent = self."createIndent"( level )

    .pcc_begin_return
    .return indent
    .pcc_end_return
.end

=item (subindent,indent) = style."newIndent"()

=cut

.sub newIndent method
    .local pmc temp
    .local string indent1
    .local string indent2
    .local int level
        
    getprop temp, "level", self
    set level, temp
    inc temp

    indent1 = self."createIndent"( level )
    inc level
    indent2 = self."createIndent"( level )

    .pcc_begin_return
    .return indent2
    .return indent1
    .pcc_end_return
.end

=item indent = style."deleteIndent"()

=cut

.sub deleteIndent method
    .local pmc temp
    .local string indent
    .local int level
        
    getprop temp, "level", self
    dec temp
    set level, temp

    indent = self."createIndent"( level )

    .pcc_begin_return
    .return indent
    .pcc_end_return
.end

=item style."dump"( name, dump )

=cut

.sub dump method
    .param string paramName
    .param pmc dump
    .local int type
    .local int ret
    .local string temp
    .local string func
    .local string name

    (type, name) = self."cache"( paramName, dump )

    if type == -1 goto NOT_IN_CACHE
    # name found in cache:
    self."dumpCached"( name, dump )
    branch END

NOT_IN_CACHE:
    # is dump null?
    # lookup the type id otherwise
    set func, "pmcNull"
    isnull dump, DUMP

    # does a helper method exists?
    typeof temp, dump
    set func, "pmc"
    concat func, temp
    can ret, self, func
    if ret goto DUMP
    # no, dump as a unknown PMC
    set func, "pmcDefault"
DUMP:
    ret = self.func( name, dump )
    unless ret goto END

    self."dumpProperties"( name, dump )

END:
    .pcc_begin_return
    .return 1
    .pcc_end_return
.end

=back

=head1 AUTHOR

Jens Rieks E<lt>parrot at jensbeimsurfen dot deE<gt> is the author
and maintainer.
Please send patches and suggestions to the Perl 6 Internals mailing list.

=head1 COPYRIGHT

Copyright (c) 2004, the Perl Foundation.

=cut
