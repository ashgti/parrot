# Copyright (C) 2009, Parrot Foundation.
# $Id$


grammar Ops::Grammar is PCT::Grammar;

token TOP {
    <header>
    <ops>
    [ $ || <panic: 'Syntax error'> ]
    {*}
}

# Cheat slightly. All ops have "VERSION = PARROT_VERSION;".
# So, capture everything before <version> as header.
token header {
    <version> {*}
}

token version {
    'VERSION = PARROT_VERSION;'
}

rule ops {
    <op>* {*}
}

rule op {
    <op_type>? 'op' <op_name=identifier> :: '(' 
        [ <op_params>? || <panic: "Fail to parse params"> ]
    ')' <op_flag>* 
    ::
    [ <op_body> || <panic: "Fail to parse op body"> ]
    {*}
}

token op_type {
    [ 'inline' | 'function' ]
}

rule op_params {
    <op_param> [ ',' <op_param> ]*
    {*}
}

rule op_param {
    <op_param_direction> <op_param_type>
    {*}
}

token op_param_direction {
    # Order is crucial. PGE doesn't support LTM yet.
    [ 
    | 'inout'
    | 'inconst'
    | 'invar'
    | 'in'
    | 'out'
    ]
}

token op_param_type {
    # Order is crucial. PGE doesn't support LTM yet.
    [
    | 'INTKEY'
    | 'INT'
    | 'NUM'
    | 'STR'
    | 'PMC'
    | 'KEY'
    | 'LABEL'
    ]
}

rule op_flag {
    ':' <identifier>
}

# OpBody starts with '{' and ends with single '}' on line.
token op_body {
    '{' .*? ^^ '}' {*}
}

token identifier {
    <.ident>
}

# ws handles whitespace, pod and perl and C comments
token ws {
  [
  | \s+
  | '#' \N*
  | ^^ '=' .*? \n '=cut'
  | '/*' .*? '*/'
  ]*
}



# vim: expandtab shiftwidth=4 ft=perl6:
