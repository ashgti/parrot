# Copyright (C) 2009, Parrot Foundation.
# $Id$

grammar VTable::Grammar is PCT::Grammar;

token TOP {
    <vtable_or_section>*
    [ $ || <panic: 'Syntax error'> ]
    {*}
}

rule sections {
    {*}                    #= begin
    <vtable_or_section>*
    {*}                    #= end
}

rule vtable_or_section {
    [ <vtable_func> | <section> ]
}

rule section {
    '[' <identifier> ']' <attribute>*               {*}
}

rule attribute {
    ':' <identifier>
}


rule vtable_func {
    <type> <identifier> '(' <args> ')' <attribute>*  {*}
}

rule type {
    [ 'struct' | 'union' ]? <identifier> '*'?
}

rule args {
    [ <arg> [ ',' <args> ]?
    |
    ]
}

rule arg {
    <type> <identifier>
}

token identifier {
    <.ident>
}

token ws {
    [ '#' \N* \n
    | \s+
    ]*
}
    
