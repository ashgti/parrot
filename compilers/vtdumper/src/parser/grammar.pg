# Copyright (C) 2009, Parrot Foundation.
# $Id$

grammar VTable::Grammar is PCT::Grammar;

token TOP {
    <sections>
    [ $ || <panic: 'Syntax error'> ]
    {*}
}

rule sections {
    {*}                    #= begin
    <vtable_or_section>*
    {*}                    #= end
}

rule vtable_or_section {
    [ <vtable_func> | <section> ]
}

rule section {
    '[' <identifier> ']' <attribute>*               {*}
}

rule attribute {
    ':' <identifier>
}

rule vtable_func {
    <type> <identifier> '(' <parameter>* ')' <attribute>*  {*}
}

rule type {
    [ 'struct' | 'union' ]? <identifier> '*'?
}

rule parameter {
    <type> <identifier> ','?
}

token identifier {
    <.ident>
}

token ws {
    [ '#' \N* \n
    | \s+
    ]*
}

