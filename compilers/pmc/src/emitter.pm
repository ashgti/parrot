# Copyright (C) 2009, Parrot Foundation.
# $Id$

# Main PMC Emitter.

class PMC::Emitter;

# Generate .h file for pmc.
method generate_h_file($past) {
    my $res;

    my $name     := $past.name();
    my $filename := self.filename();
    
    # Get emitter for (specific) PMC.
    my $pmc_emitter := get_pmc_emitter($name);

    $res :=  
            # Generate header.
              dont_edit($filename)
            # PMC functions
            ~ $pmc_emitter.generate_h_file($past)
            # C code
            ~ c_code_coda();

    $res;
}

# Generate .c file for pmc.
method generate_c_file($past) {
    my $res;

    my $name     := $past.name();
    my $filename := self.filename();
    
    # Get emitter for (specific) PMC.
    my $pmc_emitter := get_pmc_emitter($name);

    $res :=  
            # Generate header.
              dont_edit($filename)
            # PMC functions
            ~ $pmc_emitter.generate_c_file($past)
            # C code
            ~ c_code_coda();

    $res;
}

method filename() {
    our $?filename;
    $?filename;
}

method set_filename($name) {
    our $?filename := $name;
}


# Get (specific) PMC emitter
# Try to create specific emitter. In case of failure create generic one.
sub get_pmc_emitter($name) {
PIR q< 
    find_lex $P0, '$name'
    $S0 = $P0
    $P1 = new 'ResizableStringArray'
    push $P1, 'PMC'
    push $P1, 'Emitter'
    push $P1, $S0
    push_eh not_found
    %r = new $P1
    pop_eh
    goto done

  not_found:
    pop_eh
    $P1 = split '::', 'PMC::Emitter::PMC'
    %r = new $P1
  done:
>;
}

# Generate don't edit warning
sub dont_edit($filename) {
     "/* ex: set ro ft=c:\n"
    ~" * !!!!!!!   DO NOT EDIT THIS FILE   !!!!!!!\n"
    ~" *\n"
    ~" * This file is generated automatically from '" ~$filename ~"'\n"
    ~" * by Divine Intervention.\n"
    ~" *\n"
    ~" * Any changes made here will be lost!\n"
    ~" *\n"
    ~" */\n\n";
}

#=item C<c_code_coda()>
#
#Returns the Parrot C code coda
#
#=back
#
#=cut
sub c_code_coda() {
     "\n\n"
    ~"/*\n"
    ~" * Local variables:\n"
    ~" *   c-file-style: parrot\n"
    ~" * End:\n"
    ~" * vim: expandtab shiftwidth=4:\n"
    ~" */\n"
}

# Local Variables:
#   mode: perl6
#   fill-column: 100
# End:
# vim: expandtab shiftwidth=4:
